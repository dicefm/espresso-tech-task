buildscript {
    ext.compile_sdk = 29
    ext.min_sdk = 21
    ext.target_sdk = 29
    ext.version_name = '1.0'
    ext.version_code = 1
    ext.kotlin_version = '1.3.50'
    ext.dagger_version = '2.24'
    ext.navigation_version = '2.2.0-alpha03'
    ext.groupie_version = "2.5.1"
    ext.junit_version = "4.12"
    ext.mockito_version = "2.23.0"
    ext.mockito_kotlin_version = "2.1.0"

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion compile_sdk

                defaultConfig {
                    minSdkVersion min_sdk
                    targetSdkVersion target_sdk
                    versionCode version_code
                    versionName version_name
                    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
                    vectorDrawables.useSupportLibrary = true
                }

                buildTypes {
                    release {
                        minifyEnabled false
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_1_8
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}